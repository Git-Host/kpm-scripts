#!/usr/bin/env fish
function main
# ## Config
set -l PROG_NAME    "vsFTPd"
set -l REQ_STORAGE  564
set -l USERNAME     $USER



# ## Check Requirements
# ### Already installed?
if which vsftpd > /dev/null
  echo "
A previous $PROG_NAME installation is detected. It can be started with
the following command:

    \$ sudo service vsftpd start

To access your FTP server, use the FTP client of your choice with the
following information:

    HOST: $USERNAME.koding.io
    USER: $USERNAME
    PASS: YOURPASS
"
  echo "Req Error: Already installed" >&2
  exit 1
end
# ### User not root
if test "$USERNAME" = "root"
  echo "
$PROG_NAME should not be installed from the root user. Please switch
to your normal Koding user, and try this command again.
"
  echo "Req Error: Not logged in as user" >&2
  exit 1
end
# ### Storage
set -l available_storage (df / | awk 'NR>1{print $4}')
if test (math $available_storage - $REQ_STORAGE) -lt 0
  set -l hReq (math $REQ_STORAGE / 1024)
  set -l hAvl (math $available_storage / 1024)
  echo "
$PROG_NAME requires at least $hReq""MB storage, \
and you have $hAvl""MB free.

Please free up some space, and try again. You can start by deleting any
large files or directories in your home directory or delete software that
you do not need.

You can also upgrade your account to get more space, at the following url:

    https://koding.com/Pricing

Please refer to the following guide for tips on how to free up VM storage:

    http://learn.koding.com/guides/freeing-up-space
"
  echo "Req Error: Not enough storage" >&2
  exit 1
end



# ## Prompt for password
echo "
$PROG_NAME requires a user account password. This will be both your password
for '$USERNAME', as well as your FTP Password. Please enter this below.
"
stty -echo < /dev/tty
for x in (seq 3)
  echo -n "Password: "
  if not read password < /dev/tty
    stty echo < /dev/tty
    echo "Aborting after empty password" >&2
    exit 1
  end
  if test -n $password
    break
  else if test $x -eq 3
    echo -e "\nAborting after 3 empty inputs."
    stty echo < /dev/tty
    echo "Aborting after 3 password attempts" >&2
    exit 1
  else
    echo -e "\nYou must enter a password. Please retry."
  end
end
stty echo < /dev/tty



# ## Set user password
echo "$USERNAME:$password" | sudo chpasswd
or begin
  set -l err $status
  echo "Error: Unable to set user password."
  echo "Error: Unable to set user password." >&2
  exit $err
end



# ## Update
# This message is here due to a pause on apt-get update that might
# confuse the user. A vague message will help them know that it isn't
# froze.
echo -e "\nUpdating.."
sudo apt-get update
or begin
  set -l err $status
  echo "Error: Failed to update package list"
  echo "Error: apt-get update failed" >&2
  exit $err
end



# ## Install dependencies
sudo apt-get install -y vsftpd
or begin
  set -l err $status
  echo "Error: Failure installing $PROG_NAME dependencies"
  echo "Error: apt-get failed to install dependencies" >&2
  exit $err
end



# ## Success
echo "
$PROG_NAME installation complete. To access it, use the FTP client of
your choice with the following information:

    HOST: $USERNAME.koding.io
    USER: $USERNAME
    PASS: YOURPASS
"
end
main
# vim: set filetype=fish:
