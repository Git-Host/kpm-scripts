#!/usr/bin/env fish
function main
# ## Config
set -l PROG_NAME    "phpPgAdmin"
set -l REQ_STORAGE  100
set -l USERNAME     $USER



# ## Check Requirements
# ### Already installed?
if test -e /usr/share/phppgadmin
  echo "
A previous $PROG_NAME installation is detected. To access it,
visit the following link:

    $USERNAME.koding.io/phppgadmin
"
  echo "Req Error: Already installed" >&2
  exit 1
end
# ### User not root
if test "$USERNAME" = "root"
  echo "
$PROG_NAME should not be installed from the root user. Please switch
to your normal Koding user, and try this command again.
"
  echo "Req Error: Not logged in as user" >&2
  exit 1
end
# ### Storage
set -l available_storage (df / | awk 'NR>1{print $4}')
if test (math $available_storage - $REQ_STORAGE) -lt 0
  set -l hReq (math $REQ_STORAGE / 1024)
  set -l hAvl (math $available_storage / 1024)
  echo "
$PROG_NAME requires at least $hReq""MB storage, \
and you have $hAvl""MB free.

Please free up some space, and try again. You can start by deleting any
large files or directories in your home directory or delete software that
you do not need.

You can also upgrade your account to get more space, at the following url:

    https://koding.com/Pricing

Please refer to the following guide for tips on how to free up VM storage:

    http://learn.koding.com/guides/freeing-up-space
"
  echo "Req Error: Not enough storage" >&2
  exit 1
end



# ## Update
# This message is here due to a pause on apt-get update that might
# confuse the user. A vague message will help them know that it isn't
# froze.
echo -e "\nUpdating.."
sudo apt-get update
or begin
  set -l err $status
  echo "Error: Failed to update package list"
  echo "Error: apt-get update failed" >&2
  exit $err
end



# ## Install
sudo apt-get install -y phppgadmin
or begin
  set -l err $status
  echo "Error: Failure installing $PROG_NAME"
  echo "Error: apt-get failed to install $PROG_NAME" >&2
  exit $err
end



# ## Symlink
sudo ln -s /usr/share/phppgadmin /var/www/phppgadmin
or begin
  set -l err $status
  echo "Error: Failed to link $PROG_NAME install directory"
  echo "Error: Failed to symlink /var/www/phppgadmin" >&2
  exit $err
end



# ## Restart apache
sudo service apache2 restart
or begin
  set -l err $status
  echo "Error: Failed to restart apache"
  echo "Error: Failed to restart apache" >&2
  exit $err
end



# ## Success
echo "
$PROG_NAME installation complete. To access it, visit the following url:

    $USERNAME.koding.io/phppgadmin
"
end
main
# vim: set filetype=fish:

