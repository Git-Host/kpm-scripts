#!/usr/bin/env fish

# ## Config
set -l PROG_NAME    "MySQL"
set -l REQ_STORAGE  124696

# ## Check Requirements
# ### Check if already installed
if which mysql > /dev/null
  echo "
$PROG_NAME is already installed. It can be started with the following
command:

   sudo service mysql start
"
  exit 1
end
# ### Storage
set -l available_storage (df / | awk 'NR>1{print $4}')
if test (math $available_storage - $REQ_STORAGE) -lt 0
  echo "
$PROG_NAME requires at least "(math $REQ_STORAGE / 1024)"MB storage, \
and you have "(math $available_storage / 1024)"MB free.

Please free up some space, and try again. You can start by deleting any large
files or directories in your home directory or delete software that you do
not need.

You can also upgrade your account to get more space, at the following url

    https://koding.com/Pricing
"
  # Add this message to the above echo, once the guide exists
  #Please refer to the following guide to learn how to list and remove unused packages.
  exit 1
end


# ## Prompt for mysql root pass
echo -e "\nPlease enter your desired mysql root password."
stty -echo < /dev/tty
for x in (seq 3)
  echo -n "Password: "
  if not read password < /dev/tty
    stty echo < /dev/tty
    exit 1
  end
  if test -n $password
    break
  else if test $x -eq 3
    echo -e "\nAborting after 3 failed password attempts."
    stty echo < /dev/tty
    exit 1
  else
    echo -e "\nYou must enter a password. Please retry."
  end
end
stty echo < /dev/tty


# ## Install mysql
echo "
Installing MySQL..
"
if not sudo apt-get update
  echo "Error: Failure updating apt-get"
  exit 1
end
if not sudo env DEBIAN_FRONTEND=noninteractive apt-get install -q -y mysql-server
  echo "Error: Failure installing $PROG_NAME"
  exit 1
end


# ## Start mysql
# We're restarting here, to not fail if mysql was
# already running (Which it likely is, due to apt-get).
# Now, if it fails, mysql truly failed to start.
if not sudo service mysql restart
  echo "Error: MySQL failed to start."
  exit 1
end


# ## Set root user password
if not mysqladmin -u root password $password
  echo "Error: Mysqladmin failed to set root password."
  exit 1
end

echo "
Installation complete
"

# vim: set filetype=fish:
