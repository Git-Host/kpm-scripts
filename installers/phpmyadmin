#!/usr/bin/env fish

# ## FUNCTIONS
# ### Password
function get_password
  set -l password
  #stty -echo < /dev/tty
  for x in (seq 3)
    echo -n $argv[1] > /dev/tty
    if not read password < /dev/tty
      #stty echo < /dev/tty
      return 1
    end
    if test -n $password
      break
    else if test $x -eq 3
      echo -e "\nAborting after 3 empty inputs." > /dev/tty
      #stty echo < /dev/tty
      return 1
    else
      echo -e "\nYou must enter a password. Please retry." > /dev/tty
    end
  end
  #stty echo < /dev/tty
  echo $password
end



# ## Config
set -l PROG_NAME    "phpMyAdmin"
set -l REQ_STORAGE  41720
set -l DBNAME       "wordpress_db"
set -l USERNAME     $USER



# ## Check Requirements
# ### Storage
set -l available_storage
if not set available_storage (df / | awk 'NR>1{print $4}')
  echo "Error: Failed to check system size."
  exit 1
end
if test (math "$available_storage - $REQ_STORAGE") -lt 0
  echo "
$PROG_NAME requires at least "(math $REQ_STORAGE / 1024)"MB storage, \
and you have "(math $available_storage / 1024)"MB free.

Please free up some space, and try again. You can start by deleting any large
files or directories in your home directory or delete software that you do
not need.

You can also upgrade your account to get more space, at the following url

    https://koding.com/Pricing
"
  # Add this message to the above echo, once the guide exists
  #Please refer to the following guide to learn how to list and remove unused packages.
  exit 1
end
# ### Apache
if not test -e /usr/sbin/apache2
  echo "
$PROG_NAME requires that Apache is already installed. Please install
Apache first.
"
  exit 1
end
# ### MySQL
if not which mysql > /dev/null
  echo "
$PROG_NAME requires that MySQL is already installed. Please install
MySQL first.
"
  exit 1
end
# ### Existing Install
if test -e /etc/phpmyadmin
  echo "
A previous $PROG_NAME installation is detected. To access it,
visit the following link:

    $USERNAME.koding.io/phpmyadmin
"
  exit 1
end



# ## Start MySQL
# We're restarting here, to not fail if mysql was
# already running. Now, if it fails, mysql truly
# failed to start.
echo "Checking MySQL status"
if not sudo service mysql restart > /dev/null
  echo "Error: Failed to restart MySQL."
  exit 1
end



# ## Prompt for mysql root password
echo "
Please enter your MySQL root user password"
set -l mysql_pass (get_password "Password: ")
if not test "$status" -eq 0
  exit 1
end
echo ""



# ## Test mysql password
if not mysql -uroot -p$mysql_pass -e ""
  echo "Error: Invalid mysql root user authorization"
  exit 1
end



# ## Prompt for phpmyadmin app pass
echo "
Please create a phpMyAdmin app password"
set -l app_pass (get_password "Password: ")
if not test "$status" -eq 0
  exit 1
end
echo ""



# ## Configure phpmyadmin
# This configuration is being done with preseeding.
echo "
Configuring and installing phpmyadmin
"
echo "phpmyadmin phpmyadmin/dbconfig-install boolean true"             | sudo debconf-set-selections
echo "phpmyadmin phpmyadmin/reconfigure-webserver multiselect apache2" | sudo debconf-set-selections
echo "phpmyadmin phpmyadmin/mysql/admin-pass password $mysql_pass"     | sudo debconf-set-selections
echo "phpmyadmin phpmyadmin/mysql/app-pass password $app_pass"         | sudo debconf-set-selections
echo "phpmyadmin phpmyadmin/app-password-confirm password $app_pass"   | sudo debconf-set-selections



# ## Install phpmyadmin
if not sudo apt-get update
  echo "Error: Failed apt-get update"
  exit 1
end
if not sudo env DEBIAN_FRONTEND=noninteractive apt-get install -q -y phpmyadmin
  echo "Error: Failed to install phpmyadmin"
  exit 1
end



# ## Enable mcrypt
if not sudo php5enmod mcrypt
  echo "Error: Failed to enable mcrypt mod"
  exit 1
end



# ## Include the config to the end of the apache file
if not echo "Include /etc/phpmyadmin/apache.conf" | sudo tee -a /etc/apache2/apache2.conf
  echo "Error: Failed to append phpmyadmin config to apache config"
  exit 1
end



# ## Restart apache
if not sudo service apache2 restart
  echo "Error: Failed to restart apache"
  exit 1
end


echo "
Installation complete. You can access your $PROG_NAME installation at
the following address:

    $USERNAME.koding.io/phpmyadmin
"

# vim: set filetype=fish:
