#!/usr/bin/env fish
function main
# ## Config
set -l PROG_NAME    "CakePHP"
set -l REQ_STORAGE  100
set -l USERNAME     $USER
set -l INSTALL_LOC  ~/Web/cakephp



# ## Check Requirements
# ### Already installed?
if test -e $INSTALL_LOC
  echo "
A previous $PROG_NAME installation is detected. To access it,
visit the following link:

    $USERNAME.koding.io/cakephp
"
  echo "Req Error: Already installed" >&2
  exit 1
end
# ### User not root
if test "$USERNAME" = "root"
  echo "
$PROG_NAME should not be installed from the root user. Please switch
to your normal Koding user, and try this command again.
"
  echo "Req Error: Not logged in as user" >&2
  exit 1
end
# ### Apache
if not test -e /usr/sbin/apache2
  echo "
$PROG_NAME requires that Apache is already installed. Please install
Apache first.
"
  echo "Req Error: Apache not installed" >&2
  exit 1
end
# ### Composer installed?
if not which composer > /dev/null
  echo "
$PROG_NAME installation requires that composer is already installed.
Please install it with the following command:

    \$ kpm install composer
"
  echo "Req Error: composer not installed" >&2
  exit 1
end
# ### Storage
set -l available_storage (df / | awk 'NR>1{print $4}')
if test (math $available_storage - $REQ_STORAGE) -lt 0
  set -l hReq (math $REQ_STORAGE / 1024)
  set -l hAvl (math $available_storage / 1024)
  echo "
$PROG_NAME requires at least $hReq""MB storage, \
and you have $hAvl""MB free.

Please free up some space, and try again. You can start by deleting any
large files or directories in your home directory or delete software that
you do not need.

You can also upgrade your account to get more space, at the following url:

    https://koding.com/Pricing

Please refer to the following guide for tips on how to free up VM storage:

    http://learn.koding.com/guides/freeing-up-space
"
  echo "Req Error: Not enough storage" >&2
  exit 1
end



# ## Update
# This message is here due to a pause on apt-get update that might
# confuse the user. A vague message will help them know that it isn't
# froze.
echo -e "\nUpdating.."
sudo apt-get update
or begin
  set -l err $status
  echo "Error: Failed to update package list"
  echo "Error: apt-get update failed" >&2
  exit $err
end



# ## Install dependencies
sudo apt-get install -y php5-intl
or begin
  set -l err $status
  echo "Error: Failure installing $PROG_NAME dependencies"
  echo "Error: apt-get failed to install dependencies" >&2
  exit $err
end



# ## Reboot Apache
sudo service apache2 restart
or begin
  set -l err $status
  echo "Error: Apache failed to restart"
  echo "Error: Apache failed to restart" >&2
  exit $err
end



# ## Install
composer create-project --prefer-dist -s dev cakephp/app $INSTALL_LOC
or begin
  set -l err $status
  echo "Error: Failed to install $PROG_NAME"
  echo "Error: composer failed" >&2
  exit $err
end



# ## Success
echo "
$PROG_NAME installation complete. To access it, visit the following url:

    $USERNAME.koding.io/cakephp
"
end
main
# vim: set filetype=fish:

