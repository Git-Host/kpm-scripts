#!/usr/bin/env fish
function main
# ## Config
set -l PROG_NAME    "Ruby 2"
set -l REQ_STORAGE  100
set -l USERNAME     $USER



# ## Check Requirements
# ### Already installed?
if which ruby2 > /dev/null
  echo "
A previous $PROG_NAME installation is detected. You can run the
interpreter with the following command:

    \$ ruby

And install Ruby Gem packages with:

    \$ gem
"
  echo "Req Error: Already installed" >&2
  exit 84
end
# ### User not root
if test "$USERNAME" = "root"
  echo "
$PROG_NAME should not be installed from the root user. Please switch
to your normal Koding user, and try this command again.
"
  echo "Req Error: Not logged in as user" >&2
  exit 84
end
# ### Storage
set -l available_storage (df / | awk 'NR>1{print $4}')
if test (math $available_storage - $REQ_STORAGE) -lt 0
  set -l hReq (math $REQ_STORAGE / 1024)
  set -l hAvl (math $available_storage / 1024)
  echo "
$PROG_NAME requires at least $hReq""MB storage, \
and you have $hAvl""MB free.

Please free up some space, and try again. You can start by deleting any
large files or directories in your home directory or delete software that
you do not need.

You can also upgrade your account to get more space, at the following url:

    https://koding.com/Pricing

Please refer to the following guide for tips on how to free up VM storage:

    http://learn.koding.com/guides/freeing-up-space
"
  echo "Req Error: Not enough storage" >&2
  exit 84
end



# ## Update
# This message is here due to a pause on apt-get update that might
# confuse the user. A vague message will help them know that it isn't
# froze.
echo -e "\nUpdating.."
sudo apt-get update
or begin
  set -l err $status
  echo "Error: Failed to update package list"
  echo "Error: apt-get update failed" >&2
  exit $err
end



# ## Install
sudo apt-get install -y ruby2.0 ruby2.0-dev
or begin
  set -l err $status
  echo "Error: Failure installing $PROG_NAME"
  echo "Error: apt-get failed to install" >&2
  exit $err
end



# ## Make 2.0 the default
sudo ln -sf /usr/bin/ruby2.0 /usr/bin/ruby
and sudo ln -sf /usr/bin/gem2.0 /usr/bin/gem
or begin
  set -l err $status
  echo "Error: Failed to set ruby2.0 to default"
  echo "Error: Failed to set ruby2.0 and gem2.0 to default" >&2
  exit $err
end



# ## Success
echo "
$PROG_NAME installation complete. You can run the interpreter with
the following command:

    \$ ruby

And install Ruby Gem packages with:

    \$ gem
"
end
main
# vim: set filetype=fish:

