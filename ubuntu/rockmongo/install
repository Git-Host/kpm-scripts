#!/usr/bin/env fish
function main
# ## Config
set -l PROG_NAME    "Rockmongo"
set -l REQ_STORAGE  21764
set -l USERNAME     $USER
set -l INSTLOC      ~/Web/rockmongo
set -l VER          "1.1.7"



# ## Check Requirements
# ### Already installed?
if test -e "$INSTLOC"
  echo "
A previous $PROG_NAME installation is detected. To access it,
visit the following link:

    $USERNAME.koding.io/rockmongo
"
  echo "Req Error: Already installed" >&2
  exit 84
end
# ## Check if mongodb installed
if not which mongo > /dev/null
  echo "
$PROG_NAME requires that mongodb is already installed. Please install
mongodb with the following command:

    \$ kpm install mongodb
"
  echo "Req Error: Missing mongodb" >&2
  exit 84
end
# ### User not root
if test "$USERNAME" = "root"
  echo "
$PROG_NAME should not be installed from the root user. Please switch
to your normal Koding user, and try this command again.
"
  echo "Req Error: Not logged in as user" >&2
  exit 84
end
# ### Storage
set -l available_storage (df / | awk 'NR>1{print $4}')
if test (math $available_storage - $REQ_STORAGE) -lt 0
  set -l hReq (math $REQ_STORAGE / 1024)
  set -l hAvl (math $available_storage / 1024)
  echo "
$PROG_NAME requires at least $hReq""MB storage, \
and you have $hAvl""MB free.

Please free up some space, and try again. You can start by deleting any
large files or directories in your home directory or delete software that
you do not need.

You can also upgrade your account to get more space, at the following url:

    https://koding.com/Pricing

Please refer to the following guide for tips on how to free up VM storage:

    http://learn.koding.com/guides/freeing-up-space
"
  echo "Req Error: Not enough storage" >&2
  exit 84
end



# ## Update
# This message is here due to a pause on apt-get update that might
# confuse the user. A vague message will help them know that it isn't
# froze.
echo -e "\nUpdating.."
sudo apt-get update
or begin
  set -l err $status
  echo "Error: Failed to update package list"
  echo "Error: apt-get update failed" >&2
  exit $err
end



# ## Install dependencies
sudo apt-get install -y php5-dev php5-cli php-pear
or begin
  set -l err $status
  echo "Error: Failure installing $PROG_NAME dependencies"
  echo "Error: apt-get failed to install dependencies" >&2
  exit $err
end



# ## Install mongo driver
sudo pecl install mongo
or begin
  set -l err $status
  echo "Error: Failed to install $PROG_NAME driver dependencies"
  echo "Error: pecl failed to install mongo driver" >&2
  exit $err
end



# ## Append config
echo "extension=mongo.so" | sudo tee -a /etc/php5/apache2/php.ini
or begin
  set -l err $status
  echo "Error: Failed to write to php config"
  echo "Error: Failed to write mongo driver to php config" >&2
  exit $err
end



# ## Reload apache
sudo service apache2 reload
or begin
  set -l err $status
  echo "Error: Failed to reload apache"
  echo "Error: Failed to reload apache" >&2
  exit $err
end



# ## Download
git clone https://github.com/iwind/rockmongo.git "$INSTLOC"
curl -L -o "/tmp/rockmongo-$VER.tar.gz" \
  "https://github.com/iwind/rockmongo/archive/$VER.tar.gz"
or begin
  set -l err $status
  echo "Error: Failed to download $PROG_NAME"
  echo "Error: Failed to clone $PROG_NAME from the Github repo" >&2
  exit $err
end



# ## Extract and "install"
tar -C "/tmp" -xzf "/tmp/rockmongo-$VER.tar.gz"
and mv "/tmp/rockmongo-$VER" "$INSTLOC"
or begin
  set -l err $status
  echo "Error: Failed to extract $PROG_NAME"
  echo "Error: Failed to extract $PROG_NAME" >&2
  exit $err
end



# ## Cleanup
sudo rm "/tmp/rockmongo-$VER.tar.gz"
or begin
  set -l err $status
  echo "Warning $err: Failed to cleanup temp file"
  echo "Warning $err: Failed to cleanup $PROG_NAEM tar.gz file in temp dir" >&2
  # Don't exit for warnings
end




# ## Success
echo "
$PROG_NAME installation complete. To access it, visit the following url:

    $USERNAME.koding.io/rockmongo

With the following authorization:

    User: admin
    Pass: admin
"
end
main
# vim: set filetype=fish:
