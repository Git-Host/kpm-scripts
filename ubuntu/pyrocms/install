#!/usr/bin/env fish
function main
# ## Config
set -l PROG_NAME    "PyroCMS"
set -l REQ_STORAGE  100
set -l USERNAME     $USER
set -l INSTLOC      ~/Web/pyrocms
set -l VER          "2.2.5"
# Pyro zips a directory with a git hash. Meaning that
# the dir is variable based on the version zipped.
# We *could* find the specific version, but simply storing
# the tag is a bit easier.
#
# So, this should be updated each time the version is changed.
set -l ZIPDIR       "pyrocms-pyrocms-0c4d5cb"



# ## Check Requirements
# ### Already installed?
if test -e "$INSTLOC"
  echo "
A previous $PROG_NAME installation is detected. To access it,
visit the following link:

    $USERNAME.koding.io/pyrocms
"
  echo "Req Error: Already installed" >&2
  exit 84
end
# ### User not root
if test "$USERNAME" = "root"
  echo "
$PROG_NAME should not be installed from the root user. Please switch
to your normal Koding user, and try this command again.
"
  echo "Req Error: Not logged in as user" >&2
  exit 84
end
# ### Storage
set -l available_storage (df / | awk 'NR>1{print $4}')
if test (math $available_storage - $REQ_STORAGE) -lt 0
  set -l hReq (math $REQ_STORAGE / 1024)
  set -l hAvl (math $available_storage / 1024)
  echo "
$PROG_NAME requires at least $hReq""MB storage, \
and you have $hAvl""MB free.

Please free up some space, and try again. You can start by deleting any
large files or directories in your home directory or delete software that
you do not need.

You can also upgrade your account to get more space, at the following url:

    https://koding.com/Pricing

Please refer to the following guide for tips on how to free up VM storage:

    http://learn.koding.com/guides/freeing-up-space
"
  echo "Req Error: Not enough storage" >&2
  exit 84
end
# ### Apache
if not test -e /usr/sbin/apache2
  echo "
$PROG_NAME requires that Apache is already installed. Please install
Apache first.
"
  echo "Req Error: Apache not installed" >&2
  exit 84
end
# ### MySQL
if not which mysql > /dev/null
  echo "
$PROG_NAME requires that MySQL is already installed. Please install
MySQL with the following command:

    \$ kpm install mysql
"
  echo "Req Error: Mysql not installed" >&2
  exit 84
end



# ## Update
# This message is here due to a pause on apt-get update that might
# confuse the user. A vague message will help them know that it isn't
# froze.
echo -e "\nUpdating.."
sudo apt-get update
or begin
  set -l err $status
  echo "Error: Failed to update package list"
  echo "Error: apt-get update failed" >&2
  exit $err
end



# ## Install dependencies
sudo apt-get install -y php5-mysql php5-mcrypt
or begin
  set -l err $status
  echo "Error: Failure installing $PROG_NAME dependencies"
  echo "Error: apt-get failed to install dependencies" >&2
  exit $err
end



# ## Enable mcrypt
sudo php5enmod mcrypt
or begin
  set -l err $status
  echo "Error: Failed to enable mcrypt mod"
  echo "Error: Failed to enable mcrypt mod" >&2
  exit $err
end



# ## Reload Apache
sudo service apache2 reload
or begin
  set -l err $status
  echo "Error: Failed to reload Apache"
  echo "Error: Failed to reload Apache" >&2
  exit $err
end



# ## Download
curl -L -o "/tmp/pyrocms-$VER.zip" \
  "https://www.pyrocms.com/downloads/v$VER"
or begin
  set -l err $status
  echo "Error: Failed to download $PROG_NAME"
  echo "Error: Downloading zip from pyrocms.com failed" >&2
  exit $err
end



# ## Extract
unzip -d "/tmp" "/tmp/pyrocms-$VER.zip"
and mv "/tmp/$ZIPDIR" "$INSTLOC"
or begin
  set -l err $status
  echo "Error: Failed to extract $PROG_NAME"
  echo "Error: Failed to extract $PROG_NAME" >&2
  exit $err
end



 ## Configure
chmod 777 "$INSTLOC/system/cms/cache"
and chmod 777 "$INSTLOC/system/cms/config"
and chmod 777 "$INSTLOC/addons"
and chmod 777 "$INSTLOC/assets/cache"
and chmod 777 "$INSTLOC/uploads"
and chmod 777 "$INSTLOC/system/cms/config/config.php"
or begin
  set -l err $status
  echo "Error: Failed to set $PROG_NAME permissions"
  echo "Error: Failed to set $PROG_NAME permissions" >&2
  exit $err
end



# ## Cleanup
sudo rm "/tmp/pyrocms-$VER.zip"
or begin
  set -l err $status
  echo "Warning: Failed to cleanup temp file"
  echo "Warning: Failed to cleanup $PROG_NAEM zip file in temp dir" >&2
  # Don't exit for warnings
end



# ## Success
echo "
$PROG_NAME has been setup. To complete the installation, please visit
the following address:

    $USERNAME.koding.io/pyrocms
"
end
main
# vim: set filetype=fish:
