#!/usr/bin/env fish
function main
# ## Config
set -l PROG_NAME    "Cassandra"
set -l REQ_STORAGE  42316
set -l USERNAME     $USER



# ## Check Requirements
# ### Already installed?
if which cassandra > /dev/null
  echo "
A previous $PROG_NAME installation is detected. You can run the
$PROG_NAME CLI with the following command:

    \$ cassandra-cli
"
  echo "Req Error: Already installed" >&2
  exit 84
end
# ### Oracle Java7 Installed?
java -version 2>| grep -iq "se runtime"
and java -version 2>| grep -iq "1.7"
or begin
  echo "
$PROG_NAME requires Oracle Java7 to be installed. You can install it
with the following command:

    \$ kpm install oracle-java7
"
  echo "Req Error: Installation not found" >&2
  exit 84
end
# ### User not root
if test "$USERNAME" = "root"
  echo "
$PROG_NAME should not be installed from the root user. Please switch
to your normal Koding user, and try this command again.
"
  echo "Req Error: Not logged in as user" >&2
  exit 84
end
# ### Storage
set -l available_storage (df / | awk 'NR>1{print $4}')
if test (math $available_storage - $REQ_STORAGE) -lt 0
  set -l hReq (math $REQ_STORAGE / 1024)
  set -l hAvl (math $available_storage / 1024)
  echo "
$PROG_NAME requires at least $hReq""MB storage, \
and you have $hAvl""MB free.

Please free up some space, and try again. You can start by deleting any
large files or directories in your home directory or delete software that
you do not need.

You can also upgrade your account to get more space, at the following url:

    https://koding.com/Pricing

Please refer to the following guide for tips on how to free up VM storage:

    http://learn.koding.com/guides/freeing-up-space
"
  echo "Req Error: Not enough storage" >&2
  exit 84
end



# ## Add Repositories
sudo apt-add-repository 'deb http://www.apache.org/dist/cassandra/debian 21x main'
or begin
  set -l err $status
  echo "Error: Failed to add $PROG_NAME repository"
  echo "Error: apt-add-repository failed to add repos" >&2
  exit $err
end



# ## Add Keys
gpg --keyserver pgp.mit.edu --recv-keys F758CE318D77295D
and gpg --export --armor F758CE318D77295D | sudo apt-key add -
and gpg --keyserver pgp.mit.edu --recv-keys 2B5C1B00
and gpg --export --armor 2B5C1B00 | sudo apt-key add -
and gpg --keyserver pgp.mit.edu --recv-keys 0353B12C
and gpg --export --armor 0353B12C | sudo apt-key add -
or begin
  set -l err $status
  echo "Error: Failed to add $PROG_NAME keys"
  echo "Error: Failed to add $PROG_NAME keys" >&2
  exit $err
end



# ## Update
# This message is here due to a pause on apt-get update that might
# confuse the user. A vague message will help them know that it isn't
# froze.
echo -e "\nUpdating.."
sudo apt-get update
or begin
  set -l err $status
  echo "Error: Failed to update package list"
  echo "Error: apt-get update failed" >&2
  exit $err
end



# ## Install dependencies
sudo apt-get install -y cassandra
or begin
  set -l err $status
  echo "Error: Failure installing $PROG_NAME"
  echo "Error: apt-get failed to install $PROG_NAME" >&2
  exit $err
end



# ## Success
echo "
$PROG_NAME installation complete. You can run the $PROG_NAME CLI with
the following command:

    \$ cassandra-cli
"
end
main
# vim: set filetype=fish:

