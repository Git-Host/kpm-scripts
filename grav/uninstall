#!/usr/bin/env fish
function main
# ## Config
set -l PROG_NAME    "Grav"
set -l USERNAME     $USER
set -l INSTLOC      ~/Web/grav



# ## Check Requirements
# ### Check if installed
if not test -e "$INSTLOC"
  echo "
A kpm installation of $PROG_NAME cannot be found.
"
  echo "Req Error: Installation not found" >&2
  exit 84
end



# ## Confirm
echo "
By uninstalling $PROG_NAME, you will FULLY DELETE the kpm installation of
$PROG_NAME. This includes configuration and any files that the following
directory contains:

    $INSTLOC

This cannot be undone.

Are you sure?
"
for x in (seq 3)
  echo -n "(yes/no, y/n) "
  read -l confirm < /dev/tty
  or begin
    echo "Input Error: Empty confirm response" >&2
    exit 1
  end
  switch "$confirm"
    case "yes" "y"
      break
    case "no" "n"
      exit 1
    case "*"
      if test "$x" -eq 3
        echo -e "\nAborting after 3 failed attempts"
        exit 1
      else
        echo -e "\nInvalid response, please retry."
      end
  end
end



# ## Uninstall Dependencies
sudo apt-get purge --auto-remove -y php5-curl php5-gd
or begin
  set -l err $status
  echo "Error: Failed to remove $PROG_NAME dependencies"
  echo "Error: apt-get failed to uninstall dependencies" >&2
  exit $err
end



# ## Remove user groups
sudo sed -i -e "s/export APACHE_RUN_USER=$USERNAME/export APACHE_RUN_USER=www-data/g" /etc/apache2/envvars
sudo sed -i -e "s/export APACHE_RUN_GROUP=$USERNAME/export APACHE_RUN_GROUP=www-data/g" /etc/apache2/envvars



# ## Restart Apache
sudo service apache2 restart
or begin
  set -l err $status
  echo "Error: Failed to reload Apache"
  echo "Error: Failed to reload Apache" >&2
  exit $err
end



# ## Remove dir
sudo rm -rf "$INSTLOC"
or begin
  set -l err $status
  echo "Error: Failed to remove installation directory"
  echo "Error: Failed to remove installation directory, '$INSTLOC'" >&2
  exit $err
end



# ## Success
echo "
$PROG_NAME has been uninstalled successfully.
"
end
main
# vim: set filetype=fish:
