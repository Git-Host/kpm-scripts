#!/usr/bin/env fish
function main
# ## Config
set -l PROG_NAME    "Grav"
set -l REQ_STORAGE  100
set -l USERNAME     $USER
set -l INSTLOC      ~/Web/grav
set -l VER          "1.0.1"



# ## Check Requirements
# ### Already installed?
if test -e "$INSTLOC"
  echo "
A previous $PROG_NAME installation is detected. To access it,
visit the following link:

    $USERNAME.koding.io/grav
"
  echo "Req Error: Already installed" >&2
  exit 84
end
# ### User not root
if test "$USERNAME" = "root"
  echo "
$PROG_NAME should not be installed from the root user. Please switch
to your normal Koding user, and try this command again.
"
  echo "Req Error: Not logged in as user" >&2
  exit 84
end
# ### Apache
if not test -e /usr/sbin/apache2
  echo "
$PROG_NAME requires that Apache is already installed. Please install
Apache first.
"
  echo "Req Error: Apache not installed" >&2
  exit 84
end



# ## Update
# This message is here due to a pause on apt-get update that might
# confuse the user. A vague message will help them know that it isn't
# froze.
echo -e "\nUpdating.."
sudo apt-get update
or begin
  set -l err $status
  echo "Error: Failed to update package list"
  echo "Error: apt-get update failed" >&2
  exit $err
end



# ## Install dependencies
sudo apt-get install -y php5-curl php5-gd
or begin
  set -l err $status
  echo "Error: Failure installing $PROG_NAME dependencies"
  echo "Error: apt-get failed to install dependencies" >&2
  exit $err
end



# ## Setup user groups
# as directed by official Grav guide
sudo sed -i -e "s/export APACHE_RUN_USER=www-data/export APACHE_RUN_USER=$USERNAME/g" /etc/apache2/envvars
sudo sed -i -e "s/export APACHE_RUN_GROUP=www-data/export APACHE_RUN_GROUP=$USERNAME/g" /etc/apache2/envvars



# ## Restart Apache
sudo service apache2 restart
or begin
  set -l err $status
  echo "Error: Failed to reload Apache"
  echo "Error: Failed to reload Apache" >&2
  exit $err
end



# ## Download
curl -L -o "/tmp/grav-$VER.zip" \
  "https://github.com/getgrav/grav-skeleton-blog-site/releases/download/$VER/grav-skeleton-blog-site-v$VER.zip"
or begin
  set -l err $status
  echo "Error: Failed to download $PROG_NAME"
  echo "Error: Downloading zip from Grav's github failed" >&2
  exit $err
end



# ## Extract
unzip -d "/tmp" "/tmp/grav-$VER.zip"
and mv "/tmp/grav-skeleton-blog-site" "$INSTLOC"
or begin
  set -l err $status
  echo "Error: Failed to extract $PROG_NAME"
  echo "Error: Failed to extract $PROG_NAME" >&2
  exit $err
end



# ## Cleanup
sudo rm "/tmp/grav-$VER.zip"
or begin
  set -l err $status
  echo "Warning: Failed to cleanup temp file"
  echo "Warning: Failed to cleanup $PROG_NAEM zip file in temp dir" >&2
  # Don't exit for warnings
end



# ## Success
echo "
$PROG_NAME has been setup. To complete the installation, please visit
the following address:

    $USERNAME.koding.io/grav
"
end
main
# vim: set filetype=fish:
