#!/usr/bin/env fish
function main
# ## Config
set -l PROG_NAME    "Docker"
set -l REQ_STORAGE  100
set -l USERNAME     $USER



# ## Check Requirements
# ### Already installed?
if which docker > /dev/null
  echo "
A previous $PROG_NAME installation is detected. You can run it
with the following command:

    \$ docker
"
  echo "Req Error: Already installed" >&2
  exit 84
end
# ### User not root
if test "$USERNAME" = "root"
  echo "
$PROG_NAME should not be installed from the root user. Please switch
to your normal Koding user, and try this command again.
"
  echo "Req Error: Not logged in as user" >&2
  exit 84
end
# ### Storage
set -l available_storage (df / | awk 'NR>1{print $4}')
if test (math $available_storage - $REQ_STORAGE) -lt 0
  set -l hReq (math $REQ_STORAGE / 1024)
  set -l hAvl (math $available_storage / 1024)
  echo "
$PROG_NAME requires at least $hReq""MB storage, \
and you have $hAvl""MB free.

Please free up some space, and try again. You can start by deleting any
large files or directories in your home directory or delete software that
you do not need.

You can also upgrade your account to get more space, at the following url:

    https://koding.com/Pricing

Please refer to the following guide for tips on how to free up VM storage:

    http://learn.koding.com/guides/freeing-up-space
"
  echo "Req Error: Not enough storage" >&2
  exit 84
end




# ## Add the repo
echo "deb https://get.docker.com/ubuntu docker main" | sudo tee "/etc/apt/sources.list.d/docker.list"
or begin
  set -l err $status
  echo "Error: Failed to add Docker apt repository"
  echo "Error: Failed to add Docker apt repository" >&2
  exit $err
end



# ## Import the repo key
sudo apt-key adv --keyserver "hkp://keyserver.ubuntu.com:80" --recv-keys "36A1D7869245C8950F966E92D8576A8BA88D21E9"
or begin
  set -l err $status
  echo "Error: Failed to add Docker repository key"
  echo "Error: Failed to add Docker repository key" >&2
  exit $err
end



# ## Update
# This message is here due to a pause on apt-get update that might
# confuse the user. A vague message will help them know that it isn't
# froze.
echo -e "\nUpdating.."
sudo apt-get update
or begin
  set -l err $status
  echo "Error: Failed to update package list"
  echo "Error: apt-get update failed" >&2
  exit $err
end



# ## Install dependencies
sudo apt-get install -y lxc-docker
or begin
  set -l err $status
  echo "Error: Failure installing $PROG_NAME dependencies"
  echo "Error: apt-get failed to install dependencies" >&2
  exit $err
end



# ## Success
echo "
$PROG_NAME installation complete. You can run it with the following
command:

    \$ docker
"
end
main
# vim: set filetype=fish:

