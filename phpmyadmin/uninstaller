#!/usr/bin/env fish
function main



# ## Config
set -l PROG_NAME    "phpMyAdmin"
set -l USERNAME     $USER



# ## Check Requirements
# ### Is it installed?
if not test -e /etc/phpmyadmin
  echo "Error: A $PROG_NAME installation cannot be found."
  echo "Req Error: Cannot find install" >&2
  exit 84
end



# ## Remove database
# If mysql is installed, remove database. If it's not, ignore db
if which mysql > /dev/null
  # ### Get MySQL User Password
  stty -echo < /dev/tty
  for x in (seq 3)
    echo -n "
Enter your MySQL root user password: "
    if not read password < /dev/tty
      stty echo < /dev/tty
      exit 1
    end
    if test -n $password
      break
    else if test $x -eq 3
      echo -e "\nAborting after 3 empty inputs."
      stty echo < /dev/tty
      exit 1
    else
      echo -e "\nYou must enter a password. Please retry."
    end
  end
  stty echo < /dev/tty

  # We're not testing the login here, since we're not requiring
  # that this section passes. This design may change in the future.
  mysql -uroot -p"$password" -e "DROP DATABASE phpmyadmin;"
  or begin
    set -l err $status
    echo "Warning: Removing database failed"
    echo "Warning: Drop database failed with exit $err" >&2
  end
end




# ## Uninstall
sudo env DEBIAN_FRONTEND=noninteractive apt-get purge -y phpmyadmin
or begin
  set -l err $status
  echo "Error: Failed to uninstall $PROG_NAME"
  echo "Error: apt-get purge failed" >&2
  exit $err
end



# ## Autoremove
sudo apt-get -y autoremove
or begin
  set -l err $status
  echo "Error: Failed to cleanup after $PROG_NAME"
  echo "Error: apt-get autoremove failed" >&2
  exit $err
end



# ## Remove apache config
#
# Warning: Sed does not return a non-zero exit status if it cannot
# find the pattern. As a result, we don't know if this works. A
# proper method of failure needs to be figured out.
#
# Note: We are replacing \nfoo so that we only replace unmodified
# strings. If the user adds #foo or anything, this will not be replaced.
# That is by design, since we want to be careful in how we modify
# the user config.
if test -e /etc/apache2/apache2.conf
  set -l include_str "Include \/etc\/phpmyadmin\/apache.conf"
  # This is greedy, and replace all instances (including in comments/etc)
  # Ideally we want to use something else. \nfoo is hard to match though.
  sudo sed -i -e "s/$include_str//g" /etc/apache2/apache2.conf
end



# ## Success
echo "
$PROG_NAME has been uninstalled successfully.
"


end
main
# vim: set filetype=fish:
